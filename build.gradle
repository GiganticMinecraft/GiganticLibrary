buildscript {
    ext.kotlin_version = "1.2.21"
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.org.spongepowered:spongegradle:0.8.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "kotlin"
apply plugin: "org.spongepowered.plugin"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion
}

repositories {
    mavenCentral()
    repositories {
        maven { url "https://jitpack.io" }
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    compile "org.spongepowered:spongeapi:7.0.0-SNAPSHOT"
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "com.github.CrushedPixel:PacketGate:0.1.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}
